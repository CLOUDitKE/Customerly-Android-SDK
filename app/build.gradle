buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath 'me.tatarka:gradle-retrolambda:3.4.0'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'me.tatarka.retrolambda' //https://github.com/evant/gradle-retrolambda

ext {
    bintrayRepo = 'maven'
    bintrayName = 'customerly-android-sdk'

    publishedGroupId = 'io.customerly'
    libraryName = 'Customerly Android SDK'
    artifact = 'customerly-android-sdk'

    libraryDescription = 'Customerly Android SDK for integration in android app. See more at: https://www.customerly.io/'

    siteUrl = 'https://github.com/customerly/Customerly-Android-SDK'
    gitUrl = 'https://github.com/customerly/Customerly-Android-SDK.git'

    libraryVersion = 'ALPHA-0.9'

    developerId = 'giannign1'
    developerName = 'Gianni Genovesi'
    developerEmail = 'giannign1@gmail.com'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    resourcePrefix "io_customerly__"
    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 25
        versionCode 1
        versionName 'ALPHA-0.9'
    }
    buildTypes {
        all {
            buildConfigField "String", "CUSTOMERLY_WEB_SITE", "\"https://www.customerly.io/\""
            buildConfigField "String", "CUSTOMERLY_SDK_NAME", "\"Customerly\""
            buildConfigField "int", "CUSTOMERLY_API_VERSION", "1"
            buildConfigField "int", "CUSTOMERLY_SOCKET_VERSION", "1"
            buildConfigField "boolean", "CUSTOMERLY_DEV_MODE", "false"
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility "1.8"
        targetCompatibility "1.8"
    }
    dexOptions {
        preDexLibraries = false
        javaMaxHeapSize "16g"
    }
}

dependencies {
    compile 'com.squareup.okhttp3:okhttp-ws:3.4.1'
    compile ('io.socket:socket.io-client:0.7.0') {
        // excluding org.json which is provided by Android
        exclude group: 'org.json', module: 'json'
    }
    compile 'org.jetbrains:annotations:13.0'
    compile 'com.android.support:cardview-v7:25.1.0'
    compile 'com.android.support:recyclerview-v7:25.1.0'

    compile 'com.android.support:appcompat-v7:25.1.0'
    compile 'com.android.support:design:25.1.0'
}

apply from: 'https://raw.githubusercontent.com/nuuneoi/JCenter/master/installv1.gradle'
apply from: 'https://raw.githubusercontent.com/nuuneoi/JCenter/master/bintrayv1.gradle'